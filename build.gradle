import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

buildscript {
	repositories {
		mavenLocal()
		jcenter()
		mavenCentral()
	}
}

plugins {
	id "org.openapi.generator" version "4.3.1"
	id "com.moowork.node" version "1.3.1"
}

Collection<File> generatedFiles = [
		file("$project.projectDir/client-ts/api"),
		file("$project.projectDir/client-ts/model"),
		file("$project.projectDir/client-ts/api.module.ts"),
		file("$project.projectDir/client-ts/configuration.ts"),
		file("$project.projectDir/client-ts/encoder.ts"),
		file("$project.projectDir/client-ts/variables.ts"),
]

File distDirectory = file("$project.projectDir/dist")

task cleanGenerated(type: Delete) {
	delete(generatedFiles)
}

task cleanCompiled(type: Delete) {
	delete(distDirectory)
}

task clean {
	dependsOn(cleanGenerated, cleanCompiled)
}

task publish {
	dependsOn(npm_publish)
}

ext {
	specFile = file("$projectDir/api.spec.yaml")
}

task generateApi(type: GenerateTask, group: 'build') {
	inputs.file(specFile)
	outputs.files(generatedFiles)

	generatorName = "typescript-angular"
	inputSpec = specFile.toString()
	skipValidateSpec = true
	additionalProperties = [
			modelPropertyNaming: "original",
	]
	outputDir = 'client-ts/'
}

node {
	// Set the work directory for unpacking node
	workDir = file("${project.buildDir}/nodejs")

	// Set the work directory for NPM
	npmWorkDir = file("${project.buildDir}/npm")

	// Set the work directory for Yarn
	yarnWorkDir = file("${project.buildDir}/yarn")

	// Set the work directory where node_modules should be located
	nodeModulesDir = file("${project.projectDir}/client-ts")
}
